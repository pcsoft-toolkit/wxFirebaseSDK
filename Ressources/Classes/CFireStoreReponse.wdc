#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : CFireStoreReponse
 major_version : 25
 minor_version : 0
 type : 4
 description : ""
 subtype : 0
class :
 identifier : 0x175368b5000c7109
 internal_properties : BQAAAAUAAAB2/vstTMCJbS/hlxjFxirSqKvCUuv8YxgpWyl7S3iA
 code_elements :
  type_code : 10
  p_codes :
   -
     code : |1+
      // URL de la documentation sur les erreurs https://firebase.google.com/docs/firestore/use-rest-api?hl=fr#error_codes
      
      CONSTANT
      	CONST_ARGUMENT_INVALIDE				= "INVALID_ARGUMENT"
      	CONST_NONAUTHENTIFIÉ    			= "UNAUTHENTICATED"
      	CONST_INDISPONIBLE					= "UNAVAILABLE"
      	CONST_QUOTA_EPUISEE			        = "RESOURCE_EXHAUSTED"
      	CONST_AUTORISATION_REFUSEE			= "PERMISSION_DENIED"
      	
      FIN
      
      CFireStoreReponse est une Classe
      PRIVÉ 
      	m_eErreurType					est une ETypeErreur
      	m_sbErreurWindevMessage			est une chaîne
      	m_jsonDonneesDocument			est un json
      	m_bErrArgumentInvalide			est un booléen  = faux
      	m_bErrNonAuthentifie			est un booléen 	= Faux
      	m_bErrIndisponible				est un booléen  = Faux
      	m_bErrQuotaEpuisee				est un booléen  = Faux
      	m_bErrServeurIndisponible		est un booléen  = Faux
      	m_bErrAutorisationRefusee		est un booléen  = Faux
      	m_jsonDocumentTemp				est json
      fin
     type : 131072
  procedures :
   -
     name : Constructeur
     procedure_id : 1680802212538577161
     type_code : 27
     code : |1-
      procédure Constructeur(ohttpRéponse est un httpRéponse = Null, sExceptionMessage est une chaîne = "")
      
      SI ohttpRéponse = Null ALORS RETOUR
      
      SELON ohttpRéponse.CodeEtat 
      
      	CAS  0   
      		m_eErreurType			= errWindev
      		m_sbErreurWindevMessage	= sExceptionMessage
      		
      	CAS 200
      		m_eErreurType			= errAucune
      		m_jsonDonneesDocument	= ohttpRéponse.Contenu	
      		
      	CAS 400,403
      		m_eErreurType = errFirebase
      		vJsonErrResultat est un variant = JSONVersVariant(ohttpRéponse.Contenu)
      		
      		SELON vJsonErrResultat.error.status
      			CAS CONST_ARGUMENT_INVALIDE
      				m_bErrArgumentInvalide = Vrai
      			CAS CONST_NONAUTHENTIFIÉ
      				m_bErrNonAuthentifie = Vrai
      			CAS CONST_QUOTA_EPUISEE
      				m_bErrQuotaEpuisee = Vrai
      			CAS CONST_INDISPONIBLE
      				m_bErrServeurIndisponible = Vrai
      			CAS CONST_AUTORISATION_REFUSEE
      				m_bErrAutorisationRefusee = Vrai
      
      		FIN
      
      		
      FIN
     type : 589824
   -
     name : Destructeur
     procedure_id : 1680802212538642697
     type_code : 28
     code : |1+
      procédure Destructeur()
      
     type : 655360
   -
     name : _ParcoursMembresEnfant
     procedure_id : 1681514850709157509
     type_code : 12
     code : |1-
      procédure privée _ParcoursMembresEnfant(local sNomMembreParent chaine, jsonMembreEnfant est json, local sNoeudTableau est une chaîne = "")
      
      POUR TOUT UnMembreEnfant DE jsonMembreEnfant..Membre
      	SI UnMembreEnfant..Membre..Occurrence = 0 ALORS
      		
      		{"m_jsonDocumentTemp.[%sNomMembreParent%]", indVariable} = UnMembreEnfant..Valeur
      		
      		SI sNoeudTableau = EnumTableau..Valeur ALORS 
      			{"m_jsonDocumentTemp.[%sNomMembreParent%]", indVariable} = _ConstruireTableauDepuisNœud(UnMembreEnfant..Valeur)
      		fin
      	SINON
      		
      		sNomMembreParentTemp	est une chaîne	= UnMembreEnfant..Nom
      		sTypeTableau			est une chaîne	= ""
      		
      		SI UnMembreEnfant..Nom = EnumTableau..Valeur ALORS
      			sNomMembreParentTemp	= sNomMembreParent
      			sTypeTableau			= EnumTableau..Valeur
      		FIN
      		
      		SI UnMembreEnfant..Nom = EnumObjet..Valeur ALORS
      			_ConstruireObjetDepuisNœud(sNomMembreParent, UnMembreEnfant)
      		SINON
      			_ParcoursMembresEnfant(sNomMembreParentTemp, UnMembreEnfant, sTypeTableau)
      		FIN
      	FIN
      FIN
      
      	PROCÉDURE INTERNE _ConstruireTableauDepuisNœud( jsonMembreTableau)
      	
      	tabListeDesValeur est un tableau de chaînes
      	
      	POUR TOUT UneTabEnfant DE jsonMembreTableau
      		POUR TOUT x DE UneTabEnfant..Membre
      			tabListeDesValeur.Ajoute( x..Valeur)
      		FIN
      	FIN	
      	
      	RENVOYER tabListeDesValeur
      	FIN
      	
      	
      	PROCÉDURE INTERNE _ConstruireObjetDepuisNœud(LOCAL sNomNoeudParent est une chaîne, jsonMembreObjet)
      	sCleNlleObjet est une chaîne
      	
      	POUR TOUT NoeudCourant DE jsonMembreObjet..Membre
      		ParcoursObjet(NoeudCourant)
      	FIN
      	
      	
      		PROCÉDURE INTERNE ParcoursObjet(ObjetCourant)
      		
      		
      		POUR TOUT Membre DE ObjetCourant..Membre
      			SI Membre..Membre..Occurrence = 0 ALORS
      				{"m_jsonDocumentTemp.[%sCleNlleObjet%]", indVariable} =  Membre..Valeur
      			SINON
      				sCleNlleObjet = sNomNoeudParent+"."+Membre..Nom
      				ParcoursObjet( Membre)
      			FIN
      		FIN
      		FIN
      	FIN
     type : 458752
  properties :
   -
     name : errArgumentInvalide
     identifier : 0x1755b6cd00a17d52
     type_code : 103
     p_codes :
      -
        code : |1-
         procédure publique errArgumentInvalide():booléen
         
         renvoyer m_bErrArgumentInvalide
        type : 1966080
     template_refs : []
   -
     name : errNonAuthentifie
     identifier : 0x1755b8af00a2d6ba
     type_code : 103
     p_codes :
      -
        code : |1-
         procédure publique errNonAuthentifie():booléen
         
         renvoyer m_bErrNonAuthentifie
        type : 1966080
     template_refs : []
   -
     name : errIndisponible
     identifier : 0x1755b93500a3e41e
     type_code : 103
     p_codes :
      -
        code : |1-
         procédure publique errIndisponible():booléen
         
         renvoyer m_bErrIndisponible
        type : 1966080
     template_refs : []
   -
     name : errQuotaEpuisee
     identifier : 0x1755ba3f00a4f0bd
     type_code : 103
     p_codes :
      -
        code : |1-
         procédure publique errQuotaEpuisee():booléen
         
         renvoyer m_bErrQuotaEpuisee
        type : 1966080
     template_refs : []
   -
     name : errType
     identifier : 0x1755bb1100a52675
     type_code : 103
     p_codes :
      -
        code : |1-
         procédure publique errType():ETypeErreur 
         
         RENVOYER m_eErreurType
        type : 1966080
     template_refs : []
   -
     name : Données
     identifier : 0x1755e5d0010f2252
     type_code : 103
     p_codes :
      -
        code : |1-
         procédure publique Données():json
         
         
         POUR TOUT UnMembreDocument de m_jsonDonneesDocument..Membre
         	
         	SI UnMembreDocument..Membre..Occurrence = 0 ALORS
         		SI UnMembreDocument..Nom = "name" ALORS				
         			{"m_jsonDocumentTemp.id", indVariable} = ExtraitChaîne(UnMembreDocument..Valeur, 7, "/")			
         		SINON
         			{"m_jsonDocumentTemp.[%UnMembreDocument..Nom%]", indVariable} = UnMembreDocument..Valeur			
         		FIN
         	SINON
         		
         		_ParcoursMembresEnfant(UnMembreDocument..Nom, UnMembreDocument)
         		
         	FIN
         FIN
         
         renvoyer m_jsonDocumentTemp
        type : 1966080
     template_refs : []
   -
     name : errWindevMessage
     identifier : 0x1758318d0018c463
     type_code : 103
     p_codes :
      -
        code : |1-
         procédure publique errWindevMessage():chaine
         
         renvoyer m_sbErreurWindevMessage
        type : 1966080
     template_refs : []
   -
     name : errServeurIndisponible
     identifier : 0x1758378a001b2753
     type_code : 103
     p_codes :
      -
        code : |1+
         procédure publique errServeurIndisponible():booléen
         renvoyer m_bErrServeurIndisponible
        type : 1966080
     template_refs : []
   -
     name : errAutorisationRefusee
     identifier : 0x175838b5007eb93f
     type_code : 103
     p_codes :
      -
        code : |1+
         procédure publique errAutorisationRefusee():booléen
         
         renvoyer m_bErrAutorisationRefusee
         
        type : 1966080
     template_refs : []
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : BQAAAAUAAAA6ih3UbgNXHwTtiPSFUEj+2fi/m7v4QV2rqidAupM=
  original_name : Classe1
resources :
 string_res :
  identifier : 0x175365aa00048b71
  internal_properties : BQAAAAUAAAAnMYFQ1bL/vz9ehh7L22SNNSlIzGTOI8h5F/WtgDNP
custom_note :
 internal_properties : BQAAAAUAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
