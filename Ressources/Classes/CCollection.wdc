#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : CCollection
 major_version : 25
 minor_version : 0
 type : 4
 description : ""
 subtype : 0
class :
 identifier : 0x1730cc22002c0a98
 internal_properties : BQAAAAUAAAB2/vstTMCJbS/hlxjFxirSqKvCUuv8YxgpWyl7S3iA
 code_elements :
  type_code : 10
  p_codes :
   -
     code : |1+
      CCollection est une Classe hérite de CGenerateurRequeteFireStore
      	prive
      	m_sUrl			est une chaîne
      	m_sCollectionID	est une chaîne
      	m_stHttpOption	est STHttpOption
      	jsonDonneesConstruites est un JSON
      	m_HttpClientReponse est un restRéponse
      		
      		
      fin
     type : 131072
  procedures :
   -
     name : Constructeur
     procedure_id : 1671059908158360216
     type_code : 27
     code : |1-
      procédure Constructeur(sUrl est une chaîne)
      
      Constructeur CGenerateurRequeteFireStore()
      
      m_sCollectionID = ExtraitChaîne(sUrl, rangDernier, "/")
      
      m_sUrl = sUrl
     type : 589824
   -
     name : Destructeur
     procedure_id : 1671059908158425752
     type_code : 28
     code : |1+
      procédure Destructeur()
      
     type : 655360
   -
     name : Afficher
     procedure_id : 1671061608965674578
     type_code : 12
     code : |1-
      procédure Afficher(sDocumentID est une chaîne <utile> = ""):CFireStoreReponse
      
      m_sUrl+= ["/"]+"[%sDocumentID%]"
      
      m_stHttpOption.eMethode						= eHTTPGet
      m_stHttpOption.taEntête["Authorization"]	= "Bearer "+CAuth.ExposeToken
      m_stHttpOption.bufContenu					= ""
      m_HttpClientReponse							= CHttpClient.Envoie(m_sUrl, m_stHttpOption)
      
      SI ErreurDétectée() _OU_ m_HttpClientReponse.CodeEtat <> 200 ALORS
      	RENVOYER  new CFireStoreReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      RENVOYER new CFireStoreReponse(m_HttpClientReponse)
     type : 458752
   -
     name : Creer
     procedure_id : 1671061608965740349
     type_code : 12
     code : |1+
      procédure Creer(LOCAL JsonDonnees est JSON, LOCAL sNomNoeudEnfant est chaîne <utile> = ""):CFireStoreReponse
      
      m_stHttpOption.eMethode						= eHTTPPost
      m_stHttpOption.taEntête["Authorization"]	= "Bearer "+CAuth.ExposeToken
      m_stHttpOption.bufContenu					= _GénèreDocumentFirestore(JsonDonnees,sNomNoeudEnfant)
      
      m_HttpClientReponse							= CHttpClient.Envoie(m_sUrl, m_stHttpOption)
      
      SI ErreurDétectée() _OU_ m_HttpClientReponse.CodeEtat <> 200 ALORS
      	RENVOYER  new CFireStoreReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      renvoyer new CFireStoreReponse(m_HttpClientReponse)
     type : 458752
   -
     name : Modifier
     procedure_id : 1671061608965805979
     type_code : 12
     code : |1-
      procédure Modifier(LOCAL sDocumentID est une chaîne, LOCAL jContent est JSON, LOCAL sNomNoeudEnfant est chaîne,stQueryParams est une STUpdQueryParams):CFireStoreReponse
      
      SI jContent..Type PAS DANS ( wlVariantObjet,wlVariantTableau) ALORS RENVOYER  new CFireStoreReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      
      // Masks
      sUpdateMask			est une chaîne	= ""
      sMask				est une chaîne	= ""
      sFilters			est une chaîne	= ""
      sFields				est une chaîne	= ""
      taFields			est un tableau de chaîne
      
      // Vérifier qu'on un masque à formater
      sFields = stQueryParams.mask
      
      SI sFields DANS ("",Null) ALORS
      	POUR TOUT field DE jContent..Membre
      		sUpdateMask	= sUpdateMask+ ["&"]+"updateMask.fieldPaths=[%field..Nom%]"
      		sMask		= sMask+ ["&"]+"mask.fieldPaths=[%field..Nom%]"
      	FIN
      SINON
      	sFields.VersTableau(taFields,",")
      	POUR TOUT Rub DE taFields
      		sUpdateMask	= sUpdateMask+ ["&"]+"updateMask.fieldPaths=[%Rub%]"
      		sMask		= sMask+ ["&"]+"mask.fieldPaths=[%Rub%]"
      	FIN
      FIN
      
      sFilters = sFilters+(sUpdateMask <> "" ? "?"+sUpdateMask SINON "")
      
      SI sMask <> "" ALORS
      	sFilters = sFilters+(sUpdateMask <> "" ? "&"+sMask SINON "?"+sMask)
      FIN
      
      m_sUrl+= ["/"]+"[%sDocumentID%]"+"[%sFilters%]"
      
      m_stHttpOption.eMethode						= eHTTPPatch
      m_stHttpOption.bufContenu					= _GénèreDocumentFirestore(jContent,sNomNoeudEnfant)
      m_stHttpOption.taEntête["Authorization"]	= "Bearer "+CAuth.ExposeToken
      
      m_HttpClientReponse							= CHttpClient.Envoie(m_sUrl, m_stHttpOption)
      
      SI ErreurDétectée() _OU_ m_HttpClientReponse.CodeEtat <> 200 ALORS
      	RENVOYER  new CFireStoreReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      RENVOYER new CFireStoreReponse(m_HttpClientReponse)
     type : 458752
   -
     name : Supprimer
     procedure_id : 1671061608965871624
     type_code : 12
     code : |1-
      procédure Supprimer(local sDocumentID est une chaîne):CFireStoreReponse
      
      m_sUrl+= ["/"]+"[%sDocumentID%]"
      
      VariableRAZ(m_stHttpOption)
      
      m_stHttpOption.eMethode 					= eHTTPDelete
      m_stHttpOption.taEntête["Authorization"]	= "Bearer "+CAuth.ExposeToken
      
      m_HttpClientReponse							= CHttpClient.Envoie(m_sUrl, m_stHttpOption)
      
      SI ErreurDétectée() _OU_ m_HttpClientReponse.CodeEtat <> 200 ALORS
      	RENVOYER  new CFireStoreReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      RENVOYER new CFireStoreReponse(m_HttpClientReponse)
     type : 458752
   -
     name : Lister
     procedure_id : 1671101964494141146
     type_code : 12
     code : |1-
      procédure Lister(_sColName est une chaîne):CFireStoreReponse
      
      	PROCÉDURE INTERNE piPayload()
      	// Url
      	m_sUrl = ChaîneConstruit( CFireStore.CONST_FIRESTORE_BASE_URL , sPojetID)+[":runQuery"]
      	
      	// Select
      	Selectionner("usrid")
      	Selectionner("Email")
      	Selectionner("prds")
      	
      	// Where
      	Filtrer("usrid",NOT_EQUAL,0)
      	Filtrer("usrid",GREATER_THAN_OR_EQUAL,0)
      	
      	// Order By
      	TrierPar("usrid",DESCENDING)
      	//TrierPar("__name__",Croissant)
      	
      	// Limit
      	Limiter(1)
      	FIN
      	
      // Payload
      piPayload()
      //trace(_ConstruireRequete(_sColName))
      
      // Options
      m_stHttpOption.eMethode						= eHTTPPost
      m_stHttpOption.taEntête["Authorization"]	= "Bearer "+CAuth.ExposeToken
      m_stHttpOption.bufContenu					= _ConstruireRequete(_sColName)
      
      // Execute
      m_HttpClientReponse							= CHttpClient.Envoie(m_sUrl, m_stHttpOption)
      
      Trace(m_HttpClientReponse.CodeEtat)
      Trace(m_HttpClientReponse.Contenu)
      
      SI ErreurDétectée() _OU_ m_HttpClientReponse.CodeEtat <> 200 ALORS
      	RENVOYER  new CFireStoreReponse(m_HttpClientReponse, ErreurInfo(errMessage))
      FIN
      
      RENVOYER new CFireStoreReponse(m_HttpClientReponse)
     type : 458752
   -
     name : _GénèreDocumentFirestore
     procedure_id : 1681421284831580435
     type_code : 12
     code : |1-
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] _GénèreDocumentFirestore (<vNoeudRacine> est MembreVariant [, <sNomNoeudEnfant> est chaîne])
      //
      // Paramètres :
      //	vNoeudRacine (MembreVariant) : <indiquez ici le rôle de NoeudEnfant>
      //	sNomNoeudEnfant (chaîne ANSI - valeur par défaut="") : <indiquez ici le rôle de NomMembreParent>
      // Valeur de retour :
      // 	JSON : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      //	eTypeParent (CCollection.ETypesFirestore) : <indiquez ici le rôle de eTypeParent>
      procédure prive _GénèreDocumentFirestore(LOCAL vNoeudRacine est membrevariant, local sNomNoeudEnfant est chaine = "") 
      
      SELON vNoeudRacine..Type
      
      	CAS wlVariantObjet
      		
      		POUR TOUT vNoeudEnfant DE vNoeudRacine..Membre
      			SI vNoeudEnfant <> Null ET vNoeudEnfant..Type <> 0 ALORS
      				_GénèreDocumentFirestore(vNoeudEnfant, sNomNoeudEnfant = "" ? vNoeudEnfant..Nom SINON "[%sNomNoeudEnfant%].[%EnumObjet..Valeur%].[%CONST_FIELDS%].[%vNoeudEnfant..Nom%]")
      			FIN
      		FIN
      		
      	CAS wlVariantTableau
      		
      		POUR TOUT vElémentTableau, nIndice de vNoeudRacine..Valeur
      			
      			SI vElémentTableau <> Null ET vElémentTableau..Type <> 0 ALORS
      				_GénèreDocumentFirestore(vElémentTableau, sNomNoeudEnfant + ".[%EnumTableau..Valeur%].[%CONST_VALUES%][[%nIndice%]]")
      			FIN
      			
      		FIN
      		
      	AUTRE CAS
      		SI sNomNoeudEnfant <> Null ALORS
      			{"jsonDonneesConstruites.[%CONST_FIELDS%].[%sNomNoeudEnfant%].[%_ObtenirValeurEnum(vNoeudRacine..Type)%]",indVariable} = vNoeudRacine..Valeur
      		FIN
      FIN
      
      procédure interne _ObtenirValeurEnum(LOCAL nTypeDefinition est un entier):chaîne
      	
      	sValeurEnum est une chaîne
      	
      	SELON nTypeDefinition
      		CAS wlBooléen
      			sValeurEnum = EnumBoolean..Valeur
      		CAS wlChaîne, wlChaîneAnsi, wlChaîneUnicode
      			sValeurEnum = EnumChaine..Valeur
      		CAS wlEntier
      			sValeurEnum = EnumEntier..Valeur
      		CAS wlRéel
      			sValeurEnum = EnumReel..Valeur
      		CAS wlTableau
      			sValeurEnum = EnumTableau..Valeur
      			
      	FIN	
      	
      	RENVOYER sValeurEnum
      fin
      
      
      renvoyer jsonDonneesConstruites
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : BQAAAAUAAAA6ih3UbgNXHwTtiPSFUEj+2fi/m7v4QV2rqidAupM=
  original_name : Classe1
resources :
 string_res :
  identifier : 0x1730cbfc0026753d
  internal_properties : BQAAAAUAAAAnMYFQ1bL/vz9ehh7L22SNNSlIzGTOI8h5F/WtgDNP
custom_note :
 internal_properties : BQAAAAUAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
